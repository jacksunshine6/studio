// Based on tests.gant but with the includeTargets of common_tests inlined
// below (and with some changes to invoke our GUI test suite instead
import com.intellij.util.containers.hash.LinkedHashMap
import org.apache.tools.ant.AntClassLoader

import static org.jetbrains.jps.idea.IdeaProjectLoader.guessHome

includeTargets << new File("${guessHome(this)}/build/scripts/utils.gant")

requireProperty("out", "$home/out")

target(check: "Ensure the environment is configured") {
  tempDir = p("teamcity.build.tempDir")
}

target(compile: "Compile project") {
  ant.delete(failonerror: false) {
    fileset(dir: "$home/reports")
    fileset(dir: "$home/bin", includes: "*.hprof")
  }

  loadProject()
  projectBuilder.targetFolder = out
  if (!isDefined("skip_build")) {
    projectBuilder.cleanOutput()
    projectBuilder.buildAll()
  }
}

private pass(String prop) {
  if (isDefined(prop)) {
    ant.jvmarg(value: "-D$prop=${p(prop)}")
  }
}

target(run_ui_tests: 'Run java tests') {
  depends([check, compile])

  def classpathFile = "$home/uitests.classpath"
  List<String> testRuntimeClasspath = projectBuilder.moduleRuntimeClasspath(findModule("community-main"), true)
  new File(classpathFile).text = testRuntimeClasspath.findAll({ new File((String)it).exists() }).join('\n')

  testcases.each { testCase ->
    ant.junit(fork: "yes", showoutput: "true", logfailedtests: "true", printsummary: "true") {
      jvmarg(value: "-Dclasspath.file=${classpathFile}")
      pass("idea.test.group")
      pass("idea.test.patterns")
      pass("idea.fast.only")
      pass("teamcity.build.tempDir")
      pass("teamcity.tests.recentlyFailedTests.file")

      System.getProperties().entrySet().each {
        if (it.key.startsWith("pass.")) {
          def trimmed = it.key.substring("pass.".length());
          jvmarg(value: "-D${trimmed}=${it.value}");
        };
      }

      if (isDefined("commonJvmArgsForTests")) {
        commonJvmArgsForTests().each { jvmarg(value: it) }
      }

      jvmarg(value: "-Dbootstrap.testcase=$testCase")

      if (isDefined("jvm_args")) {
        jvm_args.each { jvmarg(value: it) }
      }

      classpath {
        projectBuilder.moduleRuntimeClasspath(findModule("tests_bootstrap"), false).each {
          pathelement(location: it)
        }
        pathelement(location: "${jdkHome}/lib/tools.jar")
      }

      formatter(type: "plain")
      formatter(type: "xml")
      test(name: 'com.intellij.tests.BootstrapUITests')
    }
  }
}

target('sharded_ui_tests' : "(Experimental) Runs UI tests, sharding across multiple JVMs") {
  ant.taskdef(name: "uitest", loaderRef: "GANT_CONTEXT_CLASS_LOADER",  classname: "com.android.antuitest.tasks.UiTestTask")

  def classpathFile = "$home/uitests.classpath"
  List<String> testRuntimeClasspath = projectBuilder.moduleRuntimeClasspath(findModule("community-main"), true)
  new File(classpathFile).text = testRuntimeClasspath.findAll({ new File((String)it).exists() }).join('\n')

  ant.uitest(testRoot: "$home/../adt/idea/android/guiTestSrc/", packagePrefix: "com/android/tools/idea/tests/gui", classpathFile: classpathFile) {
    pass("idea.test.group")
    pass("idea.test.patterns")
    pass("idea.fast.only")
    pass("teamcity.build.tempDir")
    pass("teamcity.tests.recentlyFailedTests.file")

    System.getProperties().entrySet().each {
      if (it.key.startsWith("pass.")) {
        def trimmed = it.key.substring("pass.".length());
        jvmarg(value: "-D${trimmed}=${it.value}");
      };
    }

    if (isDefined("commonJvmArgsForTests")) {
      commonJvmArgsForTests().each { jvmarg(value: it) }
    }

    if (isDefined("jvm_args")) {
      jvm_args.each { jvmarg(value: it) }
    }

    classpath {
      projectBuilder.moduleRuntimeClasspath(findModule("tests_bootstrap"), false).each {
        pathelement(location: it)
      }
      pathelement(location: "${jdkHome}/lib/tools.jar")
    }
  }
}

target('default' : "Run all UI tests") {
  //depends([compile, run_ui_tests])
  depends([compile, sharded_ui_tests])
}

// To run just your own tests, you can for example write
//setProperty("testcases", ["com.android.tools.idea.tests.gui.layout.LayoutPreviewTest","com.android.tools.idea.tests.gui.layout.LayoutEditorTest"])
setProperty("testcases", ["com.android.tools.idea.tests.gui.GuiTestSuite"])

def args = [
        "-Xmx2048m",
        "-XX:MaxPermSize=1024m",
        "-XX:ReservedCodeCacheSize=96m",
        "-XX:+UseCodeCacheFlushing"
]

setProperty("jvm_args", args)
