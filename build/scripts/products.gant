/*
 * Copyright (C) 2015 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


import org.apache.tools.ant.BuildException

// Returns the product specific value for a given property
def pp(String property) {
  // Properties for IDEA product
  def ideaProperties = [
    prefix: "idea",
    code: "IC",
    appInfoModule: "community-resources",
    appInfoModulePath: "community-resources",
    appInfoFile: {-> "${projectBuilder.moduleOutput(findModule("community-resources"))}/idea/IdeaApplicationInfo.xml"},
    buildNumber: "IC-$snapshot",
    systemSelector: {-> "IdeaIC${p("component.version.major")}"},
    platformPrefix: null,
    bundleIdentifier: "com.jetbrains.intellij.ce",
    macAppRoot: {-> isEap() ?
                    "IntelliJ IDEA ${p("component.version.major")} CE EAP.app/Contents" :
                    "IntelliJ IDEA ${p("component.version.major")} CE.app/Contents"},
    winAppRoot: {-> "ideaIC-$snapshot"},
    linAppRoot: {-> "ideaIC-$snapshot"},
    archiveName: {-> "ideaIC-$snapshot"},
    ideJvmArgs: null,
    maySkipAndroidPlugin: true,
    relativeAndroidHome: "android",
    relativeAndroidToolsBaseHome: "android/tools-base",

    buildUpdater: false,
    excludedPlugins: [],
    customLayout: {targetDirectory -> },

    icon128: "$home/platform/icons/src/icon_CE_128.png",
    ico: "$home/platform/icons/src/idea_CE.ico",
    icns: null,
  ]

  // Properties for Android Studio product
  def studioProperties = [
    prefix: "studio",
    code: "AI",
    appInfoModule: "adt-branding",
    appInfoModulePath: "../adt/idea/adt-branding",
    appInfoFile: {-> "${projectBuilder.moduleOutput(findModule("adt-branding"))}/idea/AndroidStudioApplicationInfo.xml"},
    buildNumber: "AI-$snapshot".replaceAll("SNAPSHOT", p("buildNumber")),
    bundleIdentifier: "com.google.android.studio",
    systemSelector: {-> "AndroidStudioPreview1.2"},
    platformPrefix: "AndroidStudio",
    macAppRoot: {-> "Android Studio.app/Contents"},
    winAppRoot: {-> "android-studio"},
    linAppRoot: {-> "android-studio"},
    archiveName: { -> "android-studio-${buildNumber}"},
    ideJvmArgs: "-Didea.platform.prefix=AndroidStudio",
    maySkipAndroidPlugin: false,
    relativeAndroidHome: "../adt/idea",
    relativeAndroidToolsBaseHome: "../base",

    buildUpdater: true,
    excludedPlugins: ["ant", "ByteCodeViewer", "devkit", "eclipse", "javaFX", "uiDesigner", "xpath", "xslt-debugger"],
    customLayout: { targetDirectory ->
      layout(targetDirectory) {
        dir("gradle") {
          dir("m2repository") {
            fileset(dir: "${home}/../../prebuilts/tools/common/offline-m2")
          }
        }

        dir ("plugins") {
          dir("google-cloud-tools") {
            dir("lib") {
              jar("google-cloud-tools.jar") {
                module("google-cloud-tools")
              }
              dir("templates") {
                fileset(dir: "${home}/../studio/google/cloud/tools/resources/templates")
              }
              dir("clientTemplates") {
                fileset(dir: "${home}/../studio/google/cloud/tools/resources/clientTemplates")
              }
              fileset(dir: "${home}/../studio/google/cloud/tools/lib") {
                include(name: "**/*.jar")
              }
            }
          }

          dir("google-login") {
            dir("lib") {
              jar("google-login.jar") {
                module("google-login")
              }
              fileset(dir: "${home}/../studio/google/login/lib") {
                include(name: "**/*.jar")
              }
              // Additional libraries that need to be manually copied
              fileset(file: "${home}/../../prebuilts/tools/common/google-api-java-client/1.8.0-rc/google-api-java-client-min-repackaged.jar")
            }
          }

          dir("google-cloud-testing") {
            dir("lib") {
              jar("google-cloud-testing.jar") {
                module("google-cloud-testing")
              }
              fileset(dir: "${home}/../studio/google/cloud/testing/lib") {
                include(name: "**/*.jar")
              }
            }
          }
        }
      }
      ant.unzip(src: "${home}/../external/gradle/gradle-2.2.1-bin.zip", dest: "${targetDirectory}/gradle")
    },

    icon128: "$home/../adt/idea/adt-branding/src/artwork/icon_AS_128.png",
    ico: "$home/../adt/idea/adt-branding/src/artwork/androidstudio.ico",
    icns: "$home/../adt/idea/adt-branding/src/artwork/AndroidStudio.icns",
  ]

  // map from product name to product properties
  def productRegistry = [
    idea: ideaProperties,
    studio: studioProperties,
  ]

  def prod = System.getProperty("product");
  if (prod == null) {
    throw new BuildException("No product defined, set a system property for product (e.g. -Dproduct=idea)")
  }

  def props = productRegistry[prod]
  if (props == null) {
    throw new BuildException("Product '" + prod + "' not defined in product registry.");
  }

  return props[property];
}

